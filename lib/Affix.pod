=encoding utf-8

=head1 NAME

Affix - A Foreign Function Interface eXtension

=head1 SYNOPSIS

    use Affix qw[:all];

    # bind to exported function
    affix libm, 'floor', [Double], Double;
    warn floor(3.14159);    # 3

    # wrap an exported function in a code reference
    my $getpid = wrap libc, 'getpid', [], Int;    # '_getpid' on Win32
    warn $getpid->();                             # $$

    # bind an exported value to a Perl value
    pin( my $ver, 'libfoo', 'VERSION', Int );

=head1 DESCRIPTION

Affix is an L<FFI|https://en.wikipedia.org/wiki/Foreign_function_interface> to wrap libraries developed in other
languages (C, C++, Rust, etc.) without having to write or maintain XS.

=head2 Features

Affix supports the following features right out of the box:

=over

=item Works on Windows, macOS, Linux, BSD, and more

=item Callbacks

=item Pointers

=item Typedefs

=item Global/Exported variables

=item Libraries developed in C, C++, and Rust (and more to come!) even those with mangled symbol names

=item Aggregates such as structs, unions, and arrays

=item Passing aggregates by value

=item Nested aggregates

=item 'Smart' L<enums|Affix::Enum>

=item Tested to work all the way down to Perl 5.026 (which is ancient in my book)

=back

=head1 The Basics

Affix's API is rather simple but not lacking in power. Let's start at the very beginning, with the eponymous C<affix(
... )> function.

=head2 C<affix( ... )>

Attaches a given symbol to a named perl sub.

    affix libm, 'pow', [Double, Double] => Double;
    warn pow( 3, 5 );

    affix libc, 'puts', [Str], Int;
    puts( 'Hello' );

    affix './mylib.dll', ['output', 'write'], [Str], Int; # renamed function
    write( 'Hello' );

    affix undef, [ 'rint', 'round' ], [Double], Double; # use current process
    warn round(3.14);

    affix find_library('xxhash'), [ 'XXH_versionNumber', 'xxHash::version' ], [], UInt;
    warn xxHash::version();

Expected parameters include:

=over

=item C<lib> - required

File path or name of the library to load symbols from. Pass an explicit C<undef> to pull functions from the main
executable.

=item C<symbol_name> - required

Name of the symbol to wrap.

Optionally, you may provide an array reference with the symbol's name and the name of the wrapping subroutine.

=item C<parameters>

Provide the argument types in an array reference.

=item C<return>

A single return type for the function.

=back

On success, C<affix( ... )> returns the generated code reference which may be called directly but you'll likely use the
name you provided.

=head2 C<wrap( ... )>

Creates a wrapper around a given symbol in a given library.

    my $pow = wrap libm, 'pow', [Double, Double] => Double;
    warn $pow->(5, 10); # 5**10

Parameters include:

=over

=item C<lib> - required

File path or name of the library to load symbols from. Pass an explicit C<undef> to pull functions from the main
executable.

=item C<symbol_name> - required

Name of the symbol to wrap.

=item C<parameters>

Provide the argument types in an array reference.

=item C<return>

A single return type for the function.

=back

C<wrap( ... )> behaves exactly like C<affix( ... )> but returns an anonymous subroutine and does not pollute the
namespace with a named function.

=head2 C<pin( ... )>

    my $errno;
    pin $errno, libc, 'errno', Int;
    print $errno;
    $errno = 0;

Variables exported by a library - also referred to as "global" or "extern" variables - can be accessed using C<pin( ...
)>. The above example code applies magic to C<$error> that binds it to the integer variable named "errno" as exported
by the L<libc> library.

Expected parameters include:

=over

=item C<var>

Perl scalar that will be bound to the exported variable.

=item C<lib>

File path or name of the library to load symbols from. Pass an explicit C<undef> to pull functions from the main
executable.

=item C<symbol_name>

Name of the exported variable to wrap.

=item C<$type>

Indicate to Affix what type of data the variable contains.

=back

This is likely broken on BSD but patches are welcome.

=head1 Library Functions

Locating libraries on different platforms can be a little tricky. These are utilities to help you out.

They are exported by default but may be imported by name or with the C<:lib> tag.

=head2 C<find_library( ... )>

    my $libm = find_library( 'm' ); # /usr/lib/libm.so.6, etc.
    my $libc = find_library( 'c' ); # /usr/lib/libc.so.6, etc.
    my $bz2 = find_library( 'bz2' ); # /usr/lib/libbz2.so.1.0.8
    my $ntdll = find_library( 'ntdll' ); # C:\Windows\system32\ntdll.dll

Locates a library close to the way the compiler or platform-dependant runtime loader does. Where multiple versions of
the same shared library exists, the most recent should be returned.

=head2 C<load_library( ... )>

=head2 C<free_library( ... )>

=head2 C<list_symbols( ... )>

=head2 C<find_symbol( ... )>

=head2 C<free_symbol( ... )>

=head2 C<dlerror( )>

    my $err = dlerror( );
    say $err if $err;

Returns a human readable string describing the most recent error that occurred from C<load_library( ... )>,
C<free_library( ... )>, etc. since the last call to C<dlerror( )>.

An undefined value is returned if no errors have occured.

=head2 C<libc()>

Returns the path to the platform-dependant equivalent of the standard C library.

This may be something like C</usr/lib/libc.so.6> (Linux), C</lib/libc.so.7> (FreeBSD), C</usr/lib/libc.dylib> (macOS),
C<C:\Windows\system32\msvcrt.dll> (Windows), etc.

=head2 C<libm()>

Returns the path to the platform-dependant equivalent of the standard C math library.

This may be something like C</usr/lib/libm.so.6> (Linux), C</lib/libm.so.5> (FreeBSD), C</usr/lib/libm.dylib> (macOS),
C<C:\Windows\system32\msvcrt.dll> (Windows), etc.

=head1 Memory Functions

To help toss raw data around, some standard memory related functions are exposed here. You may import them by name or
with the C<:memory> or C<:all> tags.

=head2 C<malloc( ... )>

    my $ptr = malloc( $size );

Allocates C<$size> bytes of uninitialized storage.

=head2 C<calloc( ... )>

    my $ptr = calloc( $num, $size );

Allocates memory for an array of C<$num> objects of C<$size> and initializes all bytes in the allocated storage to
zero.

=head2 C<realloc( ... )>

    $ptr = realloc( $ptr, $new_size );

Reallocates the given area of memory. It must be previously allocated by C<malloc( ... )>, C<calloc( ... )>, or
C<realloc( ... )> and not yet freed with a call to C<free( ... )> or C<realloc( ... )>. Otherwise, the results are
undefined.

=head2 C<free( ... )>

    free( $ptr );

Deallocates the space previously allocated by C<malloc( ... )>, C<calloc( ... )>, or C<realloc( ... )>.

=head2 C<memchr( ... )>

    memchr( $ptr, $ch, $count );

Finds the first occurrence of C<$ch> in the initial C<$count> bytes (each interpreted as unsigned char) of the object
pointed to by C<$ptr>.

=head2 C<memcmp( ... )>

    my $cmp = memcmp( $lhs, $rhs, $count );

Compares the first C<$count> bytes of the objects pointed to by C<$lhs> and C<$rhs>. The comparison is done
lexicographically.

=head2 C<memset( ... )>

    memset( $dest, $ch, $count );

Copies the value C<$ch> into each of the first C<$count> characters of the object pointed to by C<$dest>.

=head2 C<memcpy( ... )>

    memcpy( $dest, $src, $count );

Copies C<$count> characters from the object pointed to by C<$src> to the object pointed to by C<$dest>.

=head2 C<memmove( ... )>

    memmove( $dest, $src, $count );

Copies C<$count> characters from the object pointed to by C<$src> to the object pointed to by C<$dest>.

=head2 C<sizeof( ... )>

    my $size = sizeof( Int );
    my $size1 = sizeof( Struct[ name => Str, age => Int ] );

Returns the size, in bytes, of the L<type|/Types> passed to it.

=head2 C<offsetof( ... )>

    my $struct = Struct[ name => Str, age => Int ];
    my $offset = offsetof( $struct, 'age' );

Returns the offset, in bytes, from the beginning of a structure including padding, if any.

=head1 Utilities

Here's some thin cushions for the rougher edges of wrapping libraries.

They may be imported by name for now but might be renamed, removed, or changed in the future.

=head2 C<DumpHex( ... )>

    DumpHex( $ptr, $length );

Dumps C<$length> bytes of raw data from a given point in memory.

This is a debugging function that probably shouldn't find its way into your code and might not be public in the future.

=head2 C<sv_dump( ... )>

=head1 Signatures

You must provide Affix with a signature which may include types and calling conventions. Let's start with an example in
C:

    bool report( const char * name, int grades[5] );

The signature telling Affix how to call this function would look like this:

    affix 'libschool', 'report', [ Str, Array[Int, 5] ] => Bool;

Incoming arguments are defined in a list: C<[ Str, Array[Int, 5] ]>

The return value follows: C<Bool>

To call the function, your Perl would look like this:

    my $promote = report( 'Alex Smithe', [ 90, 82, 70, 76, 98 ] );

See the subsections entitled L<Types|/Types> for more on the possible types and L<Calling Conventions/Calling
Conventions> for advanced flags that may also be defined as part of your signature.

=head1 Types

Affix supports the fundamental types (void, int, etc.) as well as aggregates (struct, array, union). Please note that
types given are advisory only! No type checking is done at compile or runtime.

See L<Affix::Type>.

=head2 Calling Conventions

Handle with care! Using these without understanding them can break your code!

Refer to L<the dyncall manual|https://dyncall.org/docs/manual/manualse11.html>,
L<http://www.angelcode.com/dev/callconv/callconv.html>, L<https://en.wikipedia.org/wiki/Calling_convention>, and your
local university's Comp Sci department for a deeper explanation.

After having done that, feel free to use or misuse any of the current options:

=over

=item C<This>

Platform native C++ this calls

=item C<Ellipsis>

=item C<Varargs>

=item C<CDecl>

x86 specific

=item C<STDCall>

x86 specific

=item C<MSFastcall>

x86 specific

=item C<GNUFastcall>

x86 specific

=item C<MSThis>

x86 specific, MS C++ this calls

=item C<GNUThis>

x86 specific, GNU C++ C<this> calls are C<cdecl>, but this is defined for clarity

=item C<Arm>

=item C<Thumb>

=item C<Syscall>

=back

When used in L<signatures/Signatures>, most of these cause the internal argument stack to be reset. The exceptions are
C<Ellipsis> and C<Varargs>.

=head1 Calling into the Standard Library

If you want to call a function that's already loaded, either from the standard library or from your own program, you
can omit the library value or pass and explicit C<undef>.

For example, on Unix, you could use the following code to gather the home directory and other info about the current
user:

    use Affix;
    use Data::Printer;
    typedef PwStruct => Struct [
        name  => Str,     # username
        pass  => Str,     # hashed pass if shadow db isn't in use
        uuid  => UInt,    # user
        guid  => UInt,    # group
        gecos => Str,     # real name
        dir   => Str,     # ~/
        shell => Str      # bash, etc.
    ];
    affix undef, 'getuid',   []    => Int;
    affix undef, 'getpwuid', [Int] => Pointer [ PwStruct() ];
    p( ( Pointer [ PwStruct() ] )->unmarshal( main::getpwuid( getuid() ) ) );

=head1 ABI/API versions

If you ask Affix to load symbols from 'foo', we'll will look for C<libfoo.so> under Unix, (C<libfoo.dynlib> on macOS,
and C<foo.dll> on Windows.

Most modern system require you or the user of your module to install the development package because it's recommended
to always provide an API/ABI version to a shared library, so C<libfoo.so> ends often being a symbolic link provided
only by a development package.

To avoid that, the Affix allows you to specify the API/ABI version. It can be a full version or just a part of it. (Try
to stick to Major version, some BSD code does not care for Minor.)

    use Affix;
    affix ['foo', v1], ...;       # Will try to load libfoo.so.1 on Unix
    affix ['foo', v1.2.3], ...;   # Will try to load libfoo.so.1.2.3 on Unix

=head1 Stack Size

You may control the max size of the internal stack that will be allocated and used to bind the arguments to by setting
the C<$VMSize> variable before using Affix.

    BEGIN{ $Affix::VMSize = 2 ** 16; }

This value is C<4096> by default and probably should not be changed.

=head1 See Also

All the heavy lifting is done by L<dyncall|https://dyncall.org/>.

Check out L<FFI::Platypus> for a more robust and mature FFI

L<LibUI> for a larger demo project based on Affix

L<Types::Standard> for the inspiration of the advisory types system

=head1 LICENSE

This software is Copyright (c) 2024 by Sanko Robinson <sanko@cpan.org>.

This is free software, licensed under:

  The Artistic License 2.0 (GPL Compatible)

See L<http://www.perlfoundation.org/artistic_license_2_0>.

=head1 AUTHOR

Sanko Robinson <sanko@cpan.org>

=begin stopwords

dyncall

=end stopwords

=cut

